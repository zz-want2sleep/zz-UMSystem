buildscript {
    ext {
        springBootVersion = '1.5.10.RELEASE'
        lemonVersion = '1.8.x'
        cmpayVersion = '1.8.x'
        sonarUrl = 'http://172.16.50.222:9000/'
        mavenUrl = 'http://maven.cmpay.com:8081/repository/maven-public/'
        versionMngUrl = 'http://172.16.50.222:82/snippets/2/raw'

    }
    repositories {
        maven { url "${mavenUrl}" }
    }
    apply from: versionMngUrl
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:1.0.1.RELEASE")
        classpath("com.cmpay:lemon-plugin:${lemonVersion}")
    }
}

//plugins {
//    id 'io.freefair.lombok' version '5.0.0-rc6' apply false
//}

subprojects {

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'lemon'
//    apply plugin: "io.freefair.lombok"
    version = '1.0.0'
    group = 'com.cmpay.bui'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 5, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 5, 'seconds'
    }

    repositories {
        maven { url "${mavenUrl}" }
    }

    dependencyManagement {
        imports {
            mavenBom "com.cmpay:cmpay-dependencies:${cmpayVersion}"
            mavenBom "com.cmpay:lemon-dependencies:${lemonVersion}"
        }

    }
    dependencies {
        // resolve dependencies conflicts
        compileOnly("org.springframework.boot:spring-boot-configuration-processor")
        testCompile("org.springframework.boot:spring-boot-starter-test")
    }

    //打包源代码
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    artifacts {
        archives jar
        archives sourcesJar
    }
}
configure(subprojects.findAll { it.name.matches('\\S*(-interface|-constant|-common)\\S*') }) {
    println("project name:" + project.getName())
    uploadArchives {
        repositories {
            mavenDeployer {

                snapshotRepository(url: "http://maven.cmpay.com:8081/repository/cmpay-interface-snapshots/") {
                    authentication(userName: appMavenUserSnap, password: appMavenPasswordSnap)
                }

                repository(url: "http://maven.cmpay.com:8081/repository/cmpay-interface-releases/") {
                    authentication(userName: appMavenUserRelease, password: appMavenPasswordRelease)
                }
            }
        }
    }
}
description = 'cmpay bui manager application'